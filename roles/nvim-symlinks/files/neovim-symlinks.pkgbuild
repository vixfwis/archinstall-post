# Maintainer: Doug Newgard <scimmia at archlinux dot info>
# Contributor: Tomas Storck <storcktomas@gmail.com>
# Contributor: Tom Richards <tom@tomrichards.net>
# copy: 2023-03-09

pkgname=neovim-symlinks
pkgver=5
pkgrel=1
pkgdesc='Runs neovim if vi or vim is called'
arch=('any')
depends=('neovim' 'sh')
provides=('vim' 'vi')
conflicts=('vim' 'vi')

package() {
  install -dm755 "$pkgdir/usr/bin/"
  cd "$pkgdir/usr/bin/"

  echo -e '#!/bin/sh\nexec nvim -e "$@"'  > ex
  echo -e '#!/bin/sh\nexec nvim -R "$@"'  > view
  echo -e '#!/bin/sh\nexec nvim -d "$@"'  > vimdiff
  chmod 755 *

  for _link in edit vedit vi vim; do
    ln -s nvim $_link
  done

  install -dm755 "$pkgdir/usr/share/zsh/site-functions"
  cd "$pkgdir/usr/share/zsh/site-functions"
  echo -e '#compdef edit=nvim ex=nvim vedit=nvim vi=nvim view=nvim vim=nvim vimdiff=nvim\n\n_vim' > _nvim
}
